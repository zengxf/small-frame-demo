plugins {
    id "java"
    id "io.freefair.lombok" version "8.0.1"
    id 'com.google.protobuf' version '0.9.4'
}

sourceCompatibility = 17

def grpcVersion = '1.60.0'
def protobufVersion = '3.25.1'
def protocVersion = protobufVersion

repositories {
    mavenLocal()
    maven { url "https://maven.aliyun.com/nexus/content/groups/public/" }
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}
/*
sourceSets { // 可以省（默认就是用这些配置）
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}
*/

dependencies {
    // gRPC
    implementation group: 'io.grpc', name: 'grpc-all', version: "${grpcVersion}"
    // javax
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    // Slf4j
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.7'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.7'

    // JUnit-5 (兼容 4)
    testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.9.3'
    // JUnit-5
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.3'
}

// 启用 JUnit 5
test {
    useJUnitPlatform()
}